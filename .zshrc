################################################################################
##
##  .zshrc
##      zsh ã®åˆæœŸè¨­å®š
##
################################################################################




################################################################################
##
##  ã‚·ã‚§ãƒ«å¤‰æ•°ãƒ»ç’°å¢ƒå¤‰æ•° (ç’°å¢ƒåˆ¥ã®è¨­å®šï¼Œ.zshenv ãªã©ã«æ›¸ããƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆ)
##
################################################################################
## ãƒ†ãƒ³ãƒãƒ©ãƒªãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒª
# TEMP_DIR="/tmp/${USER}";
# VAR_TEMP_DIR="/var/tmp/${USER}";
# [[ -d ${TEMP_DIR} ]] || mkdir -m 700 ${TEMP_DIR}
# [[ -d ${VAR_TEMP_DIR} ]] || mkdir -m 700 ${VAR_TEMP_DIR}
## è£œå®Œã®ã‚­ãƒ£ãƒƒã‚·ãƒ¥ãƒ•ã‚¡ã‚¤ãƒ«
# ZCOMPDUMP="${TEMP_DIR}/zcompdump"
## è¨€èªç’°å¢ƒ
# export LANG='ja_JP.eucJP'
export LANG='ja_JP.UTF-8'
## ls ã®ã‚«ãƒ©ãƒ¼è¡¨ç¤ºã‚ªãƒ—ã‚·ãƒ§ãƒ³
 LS_COLOR_OPTION='-G'
#LS_COLOR_OPTION='--color'
## ls & è£œå®Œå€™è£œã®è‰²ä»˜ã‘
##- é€šå¸¸
#export LSCOLORS='exfxcxdxbxegedabagacad'
#export LS_COLORS='di=34:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'
##- èƒŒæ™¯ãŒé»’ã§é’ãŒè¦‹ãˆã«ãã„å ´åˆ
 export LSCOLORS='gxfxcxdxbxegedabagacad'
 export LS_COLORS='di=36:ln=35:so=32:pi=33:ex=31:bd=46;34:cd=43;34:su=41;30:sg=46;30:tw=42;30:ow=43;30'

# export EDITOR='emacs'					# ã‚¨ãƒ‡ã‚£ã‚¿
export EDITOR=emacsclient
export VISUAL=emacsclient
# export PAGER='less'						# ãƒšãƒ¼ã‚¸ãƒ£
# export LESS='-R --tabs=4 --no-init --LONG-PROMPT --ignore-case'	#- less å®Ÿè¡Œæ™‚ã®ã‚ªãƒ—ã‚·ãƒ§ãƒ³
 export CC='gcc'							# C ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©
 export CXX='g++'						# C++ ã‚³ãƒ³ãƒ‘ã‚¤ãƒ©
# export SCREENDIR="${HOME}/.screen"		# screen ã®ã‚»ãƒƒã‚·ãƒ§ãƒ³ä¿å­˜å…ˆ
# export GNUTERM='x11'					# gnuplot ã®æ¨™æº–ã®ã‚¿ãƒ¼ãƒŸãƒŠãƒ«
# export OMP_NUM_THREADS=2				# OpenMP ã§ã®ã‚¹ãƒ¬ãƒƒãƒ‰æ•°
#rsenseã®ã›ã£ã¦ã„




################################################################################
##
##  zsh function
##
################################################################################
autoload -Uz zed				##- ç°¡æ˜“ãƒ†ã‚­ã‚¹ãƒˆã‚¨ãƒ‡ã‚£ã‚¿
autoload -Uz zmv				##- ãƒªãƒãƒ¼ãƒ  (zmv -W 'hoge.*' 'moge.*')
autoload -Uz colors;			##- è‰²ã®è¨˜è¿°ã‚’ç°¡å˜ã«
colors							##-- colors ã®åˆæœŸåŒ–





################################################################################
##
##  zsh option
##
################################################################################
setopt correct				##- ã‚³ãƒãƒ³ãƒ‰è£œæ­£
setopt extended_glob		##- ã‚°ãƒ­ãƒƒãƒ”ãƒ³ã‚°æ‹¡å¼µ




################################################################################
##
##  dir æ“ä½œ
##
################################################################################
##--------------------------------------
## environment variable  (p. 50)
##--------------------------------------
cdpath=( ${HOME} )			##- ã‚«ãƒ¬ãƒ³ãƒˆã«æŒ‡å®šã—ãŸ dir ãŒãªã„å ´åˆï¼Œ${cdpath} ã‚’åŸºæº–ã«ç§»å‹•ã‚’è©¦ã¿ã‚‹
DIRSTACKSIZE=20				##- dirã‚¹ã‚¿ãƒƒã‚¯ã®ã‚µã‚¤ã‚º
##--------------------------------------
## option  (pp. 50-51)
##--------------------------------------
setopt auto_cd				##- dir åã ã‘ã§ cd
setopt auto_name_dirs		##- ä»£å…¥ç›´å¾Œã‹ã‚‰åå‰ä»˜ãdirã‚’æœ‰åŠ¹ã«
setopt auto_pushd			##- dir ç§»å‹•ã§è‡ªå‹•çš„ã« push
setopt cdable_vars			##- dir ãŒå­˜åœ¨ã—ãªã„å ´åˆï¼Œåå‰ä»˜ãdirã¸ã®ç§»å‹•ã‚’è©¦ã¿ã‚‹
setopt pushd_ignore_dups	##- dirã‚¹ã‚¿ãƒƒã‚¯ã®å†…å®¹ã¨åŒã˜ dir ã‚’ push ã—ãªã„
setopt pushd_to_home		##- pushd ã®å¼•æ•°ãŒãªã„å ´åˆï¼Œ${HOME} ã¸ç§»å‹•




################################################################################
##
##  history
##
################################################################################
##--------------------------------------
## environment variable (p. 54)
##--------------------------------------
HISTFILE="${HOME}/.zsh_history"
HISTSIZE=100000
SAVEHIST=100000
##--------------------------------------
## option (pp. 68-72)
##--------------------------------------
setopt extended_history		##- history file ã‚’æ‹¡å¼µãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆã§ä¿å­˜
setopt hist_ignore_all_dups	##- history ã«é‡è¤‡ãŒã‚ã£ãŸå ´åˆã¯å¤ã„æ–¹ã‚’å‰Šé™¤
setopt hist_ignore_dups		##- ç›´å‰ã¨åŒã˜ã‚³ãƒãƒ³ãƒ‰ã‚’è¨˜éŒ²ã—ãªã„
setopt hist_ignore_space	##- å…ˆé ­ã« space ã§è¨˜éŒ²ã—ãªã„
setopt hist_reduce_blanks	##- history ã«è¿½åŠ ã™ã‚‹éš›ã«ç„¡é§„ãª space ã‚’å‰Šé™¤
setopt hist_save_no_dups	##- history file ã«ä¿å­˜ã™ã‚‹éš›ï¼Œé‡è¤‡ãŒã‚ã£ãŸå ´åˆã¯å¤ã„æ–¹ã‚’å‰Šé™¤
setopt hist_verify			##- ç·¨é›†ã—ã¦å®Ÿè¡Œã§ãã‚‹ã‚ˆã†ã«
setopt share_history		##- history ã‚’ ã™ã¹ã¦ã® zsh ãƒ—ãƒ­ã‚»ã‚¹ã§å…±æœ‰




################################################################################
##
##  command complete
##
################################################################################
## è£œå®Œæ©Ÿèƒ½ã®èª­ã¿è¾¼ã¿ã¨åˆæœŸåŒ–
autoload -Uz compinit		##- ã‚³ãƒãƒ³ãƒ‰è£œé–“
##- compinitã®åˆæœŸåŒ–
[[ -n ${ZCOMPDUMP} ]] && compinit -d ${ZCOMPDUMP} || compinit
# autoload -Uz predict-on; 	##- äºˆæ¸¬è£œé–“
# predict-on				##- predict-onã®åˆæœŸåŒ–
##--------------------------------------
## option (pp. 131-135)
##--------------------------------------
setopt always_last_prompt	##- å€™è£œä¸€è¦§ã‚’è¡¨ç¤ºã—ã¦ã‚‚å…ƒã® prompt ã«ç•™ã¾ã‚‹
setopt complete_in_word		##- æ–‡å­—åˆ—ã®é€”ä¸­ã§ã‚‚ã‚«ãƒ¼ã‚½ãƒ«ä¸€ä½ç½®ã§è£œå®Œ
setopt auto_list			##- å€™è£œãŒè¤‡æ•°ã‚ã‚‹ã¨ãï¼Œè‡ªå‹•çš„ã«ä¸€è¦§ã‚’å‡ºã™
setopt auto_menu			##- è£œå®Œã‚­ãƒ¼ã®é€£æ‰“ã§ãƒ¡ãƒ‹ãƒ¥ãƒ¼è£œå®Œã«ç§»è¡Œ
setopt auto_param_keys		##- å¤‰æ•°åã®ã‚«ãƒƒã‚³å¯¾å¿œãªã©ã‚’è‡ªå‹•çš„ã«è£œå®Œ
setopt auto_param_slash		##- dir ã®è£œå®Œã§ / ã‚’è‡ªå‹•çš„ã«ä»˜åŠ 
setopt auto_remove_slash	##- auto_param_slash ã§ä»˜åŠ ã•ã‚ŒãŸ / ã‚’ï¼Œæ¬¡ã®å…¥åŠ›ãŒãƒ‡ãƒªãƒŸã‚¿ã®å ´åˆå‰Šé™¤
setopt glob_complete		##- ã‚°ãƒ­ãƒƒãƒ“ãƒ³ã‚°ãƒ‘ã‚¿ãƒ¼ãƒ³ã‚’å±•é–‹ã›ãšã«ï¼Œãƒ¡ãƒ‹ãƒ¥ãƒ¼è£œå®Œ
setopt hash_list_all		##- è£œå®Œæ™‚ã«ã‚³ãƒãƒ³ãƒ‰ãƒãƒƒã‚·ãƒ¥ã‚’ç¢ºèª
setopt list_ambiguous
setopt list_beep			##- è£œå®ŒçµæœãŒ1ã¤ã«ç¢ºå®šã—ãªã„å ´åˆï¼Œbeep
setopt list_packed			##- è£œé–“ä¸€è¦§ã‚’è©°ã‚ã¦è¡¨ç¤º
# setopt list_rows_first		##- å€™è£œä¸€è¦§ã®ç§»å‹•ã‚’æ¨ªã«
setopt list_types			##- å€™è£œä¸€è¦§ã«ãƒ•ã‚¡ã‚¤ãƒ«ç¨®åˆ¥ã‚’è¡¨ã™è¨˜å·ã‚’ä»˜åŠ 
##--------------------------------------
## è£œå®Œã‚¹ã‚¿ã‚¤ãƒ« (pp. 135-)
##--------------------------------------
## ãƒ¡ãƒ‹ãƒ¥ãƒ¼è£œå®Œã‚’æœ‰åŠ¹ã«
zstyle ':completion:*:default' menu select=1

## è£œå®Œå€™è£œãƒ»ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãƒ»è­¦å‘Šã®å…±é€šã‚¹ã‚¿ã‚¤ãƒ«
##- è£œå®Œå€™è£œã‚’æ²¢å±±???
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list _history
		# _complete		æ™®é€šã®è£œå®Œé–¢æ•°
		# _approximate	ãƒŸã‚¹ã‚¹ãƒšãƒ«ã‚’è¨‚æ­£ã—ãŸä¸Šã§è£œå®Œã‚’è¡Œã†ï¼
		# _match		*ãªã©ã®ã‚°ãƒ­ãƒ–ã«ã‚ˆã£ã¦ã‚³ãƒãƒ³ãƒ‰ã‚’è£œå®Œã§ãã‚‹(ä¾‹ãˆã°vi* ã¨æ‰“ã¤ã¨viã¨ã‹vimã¨ã‹è£œå®Œå€™è£œãŒè¡¨ç¤ºã•ã‚Œã‚‹)
		# _expand		ã‚°ãƒ­ãƒ–ã‚„å¤‰æ•°ã®å±•é–‹ã‚’è¡Œã†ï¼ã‚‚ã¨ã‚‚ã¨ã‚ã£ãŸå±•é–‹ã¨æ¯”ã¹ã¦ã€ç´°ã‹ã„åˆ¶å¾¡ãŒå¯èƒ½
		# _history		å±¥æ­´ã‹ã‚‰
##- è£œå®Œä¸€è¦§ã®è‰²ã¥ã‘
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
##- è£œå®Œä¸€è¦§ã‚’ã‚°ãƒ«ãƒ¼ãƒ—è¡¨ç¤º
zstyle ':completion:*' group-name ''
##- ãã®ä»–
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*:messages' format '%{[33m%}%d%{[m%}'
zstyle ':completion:*:warnings' format '%{[31m%}No matches for: %{[33m%}%d%{[m%}'
zstyle ':completion:*:descriptions' format '%{[32m%}-- %d%{[m%}'

## å°æ–‡å­—ã®æ–‡å­—åˆ—ã§è£œå®Œã—ãŸå ´åˆï¼Œå¤§æ–‡å­—ã«ã‚‚ãƒãƒƒãƒã•ã›ã‚‹
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

## cd ãªã—ã® dir ç§»å‹•ã§ dir ã‚’è£œå®Œå€™è£œã®æœ€åˆã«
##- æ¤œç´¢é †
zstyle ':completion:*' tag-order local-directories 'path-directories named-directories'
##- è¡¨ç¤ºé †
zstyle ':completion:*' group-order local-directories path-directories named-directories

## cd
##- cd ã®æ¤œç´¢é †
zstyle ':completion:*:cd:*' tag-order local-directories 'path-directories named-directories'
##- cd ã®è¡¨ç¤ºé †
zstyle ':completion:*:cd:*' group-order local-directories path-directories named-directories
##- cd ã¯è¦ªdir ã‹ã‚‰ã‚«ãƒ¬ãƒ³ãƒˆdir ã‚’é¸æŠã—ãªã„ã®ã§è¡¨ç¤ºã•ã›ãªã„ã‚ˆã†ã«ã™ã‚‹ (ä¾‹: cd ../<TAB>):
zstyle ':completion:*:cd:*' ignore-parents parent pwd

## ãã®ä»–
##- ~/.ssh/configã§è¨­å®šã—ãŸãƒ›ã‚¹ãƒˆåã‚’ ssh ãªã©ã®è£œå®Œå€™è£œã«
zstyle ':completion:*:hosts' hosts




################################################################################
##
##  prompt
##
################################################################################
## ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆ
PROMPT="%n@%m: %{${fg[green]}%}%c %#%{${reset_color}%} "
PROMPT2="%{${fg[red]}%}%_%#%{${reset_color}%} "
## ã‚³ãƒãƒ³ãƒ‰ã‚’é–“é•ãˆãŸã¨ãã®ä¿®æ­£ã™ã‚‹ã‹å¦ã‹ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸
SPROMPT="correct: %R -> %r ? [n,y,a,e]: "
## å³ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆ
RPROMPT="%{${bg[green]}${fg[black]}%} %~ %{${reset_color}%}"




################################################################################
##
##  SSHç’°å¢ƒãƒ»ç«¯æœ«ç’°å¢ƒåˆ¥ã®è¨­å®š
##
################################################################################
## ssh ç’°å¢ƒç”¨
if [[ -n ${SSH_TTY} ]]; then
	##- ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆ
	PROMPT="%{${bg[red]}%}%n@%m%{${reset_color}%}: %{${fg[green]}%}%c %#%{${reset_color}%} "
fi


## screen ç’°å¢ƒç”¨
if [[ -n ${STY} ]]; then
	##- preexec ç”¨ã®ã‚³ãƒãƒ³ãƒ‰ (ä¸€è¦§è¡¨ç¤ºåã‚’ã‚³ãƒãƒ³ãƒ‰ã«å¤‰ãˆã‚‹)
	alias preexec_screen='print -n "\ek@_${1%% *}\e\\"'
	##- precmd ç”¨ã®ã‚³ãƒãƒ³ãƒ‰ (ä¸€è¦§è¡¨ç¤ºåã‚’ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã«å¤‰ãˆã‚‹)
	alias precmd_screen='print -n "\ek${PWD##*/}\e\\"'
	##- ssh ã®ãƒ›ã‚¹ãƒˆåã‚’ä¸€è¦§è¡¨ç¤ºåã«
	function ssh ()
	{
		screen -t "ssh ->${@[${#}]}" ssh "${@}"
	}
else
	alias preexec_screen=''
	alias precmd_screen=''
fi


## Emacs ç’°å¢ƒç”¨
if [[ -n ${EMACS} ]]; then
	##- Emacs ç’°å¢ƒã§ã‚³ãƒãƒ³ãƒ‰ã‚’ç¦æ­¢ã™ã‚‹
	#alias emacs='print "@emacs!!!"'
fi




################################################################################
##
##  VCS INFO (ãƒãƒ¼ã‚¸ãƒ§ãƒ³ç®¡ç†ã‚·ã‚¹ãƒ†ãƒ ã®æ”¯æ´æ©Ÿèƒ½)
##
################################################################################
## version >= 4.3.6 ãªã‚‰ vcs_info ã‚’æœ‰åŠ¹ã«
if [[ ${${(s:.:)ZSH_VERSION}[1]} -gt 4 ]]; then
	VCS_INFO_ENABLE=true
elif [[ ${${(s:.:)ZSH_VERSION}[1]} -eq 4 ]]; then
	if [[ ${${(s:.:)ZSH_VERSION}[2]} -gt 3 ]]; then
		VCS_INFO_ENABLE=true
	elif [[ ${${(s:.:)ZSH_VERSION}[2]} -eq 3 ]]; then
		[[ ${${(s:.:)ZSH_VERSION}[3]} -ge 6 ]] && VCS_INFO_ENABLE=true
	fi
fi


## æœ‰åŠ¹ãªå ´åˆã®è¨­å®š
if [[ -n ${VCS_INFO_ENABLE} ]]; then
	##- VCS INFO ã‚’æœ‰åŠ¹ã«
	autoload -Uz vcs_info
	##- å³ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆã«è¿½åŠ 
	RPROMPT="%1(v|%F{green}%1v%f|)${RPROMPT}"
	##- è¡¨ç¤ºè¨­å®š
	zstyle ':vcs_info:*' formats '[%b (%s)] %S '
	zstyle ':vcs_info:*' actionformats '[%b|%a (%s)] %S '
	##- precmd ç”¨ã®ã‚³ãƒãƒ³ãƒ‰
	alias precmd_vcs_info='LANG=en_US.UTF-8 vcs_info; psvar[1]="${vcs_info_msg_0_}"'
else
	alias precmd_vcs_info=''
fi
unset VCS_INFO_ENABLE




################################################################################
##
##  ã‚³ãƒãƒ³ãƒ‰å®Ÿè¡Œå‰ï¼Œãƒ—ãƒ­ãƒ³ãƒ—ãƒˆè¡¨ç¤ºå‰ã«å®Ÿè¡Œã•ã‚Œã‚‹é–¢æ•°
##
################################################################################
## ã‚³ãƒãƒ³ãƒ‰å®Ÿè¡Œå‰ã«å®Ÿè¡Œã•ã‚Œã‚‹
function preexec ()
{
	preexec_screen
}


## ãƒ—ãƒ­ãƒ³ãƒ—ãƒˆè¡¨ç¤ºå‰ã«å®Ÿè¡Œã•ã‚Œã‚‹
function precmd ()
{
	precmd_screen
	precmd_vcs_info
}




################################################################################
##
##  key bind
##
################################################################################
## Emacsé¢¨
bindkey -e
## å±¥æ­´æ¤œç´¢
autoload -Uz history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^P" history-beginning-search-backward-end
bindkey "^N" history-beginning-search-forward-end
## è£œå®Œãƒªã‚¹ãƒˆã§ ^P, ^N ã‚’æœ‰åŠ¹ã«
zmodload -i zsh/complist
bindkey -M menuselect \
	"^P" up-line-or-history \
	"^N" down-line-or-history




################################################################################
##
##  alias
##
################################################################################
alias history-all='history -E 1'
setopt complete_aliases
## ã‚°ãƒ­ãƒ¼ãƒãƒ«ã‚¨ã‚¤ãƒªã‚¢ã‚¹
alias -g I=/dev/stdin
alias -g O=/dev/stdout
alias -g E=/dev/stderr
alias -g N=/dev/null
alias -g LE="| ${PAGER}"
alias -g G='| grep'
alias -g X='| xargs'
## ãã®ä»–
alias where='command -v'
alias j='jobs -l'
alias rm='rm -i'
alias cp='cp -i'
alias mv='mv -i'
alias l="ls ${LS_COLOR_OPTION} -a"
alias ls="ls ${LS_COLOR_OPTION}"
alias la="ls ${LS_COLOR_OPTION} -a"
alias lf="ls ${LS_COLOR_OPTION} -F"
alias ll="ls ${LS_COLOR_OPTION} -lh"
alias lla="ls ${LS_COLOR_OPTION} -lah"
alias du='du -h'
alias df='df -h'
alias su='su -l'
alias zmv='noglob zmv'
#alias emc='emacs -nw'
alias ec='emacsclient'
#alias es='emacs -fs --iconic &'
#alias r='rails'
alias cd..='cd ..'
## nkf ã«ã‚ˆã‚‹æ–‡å­—ã‚³ãƒ¼ãƒ‰å¤‰æ›
#if ( whence nkf > /dev/null 2>&1 ); then
if [[ -x $( whence nkf ) ]]; then
	alias utf='nkf -w -Lu --overwrite'		#- utf8-jp
	alias euc='nkf -e -Lu --overwrite'		#- euc-jp
	alias sjis='nkf -s -Lu --overwrite'		#- shift-jis
fi




################################################################################
##
##  è‡ªåˆ†ç”¨ã®è¨­å®š & ãã®ä»–ã®è¨­å®šã‚’èª­ã¿è¾¼ã¿
##
################################################################################
[[ -r ~/.zshrc.local ]] && source ~/.zshrc.local
#export
export PKG_CONFIG_PATH="$PKG_CONFIG_PATH:/usr/local/lib/pkgconfig"
# MacPorts Installer addition on 2010-06-25_at_20:56:19: adding an appropriate PATH variable for use with MacPorts.
export PATH=/opt/local/bin:/opt/local/sbin:$PATH
export PYTHONPATH=/opt/local/Library/Frameworks/Python.framework/Versions/2.6/lib/python2.6/site-packages
# Finished adapting your PATH environment variable for use with MacPorts.

PATH=$PATH:$HOME/.rvm/bin # Add RVM to PATH for scripting
[[ -s "$HOME/.rvm/scripts/rvm" ]] && . "$HOME/.rvm/scripts/rvm" # Load RVM function
route add -net add default 10.1.0.0/16 10.2.128.10
